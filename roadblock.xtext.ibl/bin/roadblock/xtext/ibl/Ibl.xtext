// xtext grammar for Roadblock IBL language
// 2013, released under GNU GPL V3
// jamie.twycross AT nottingham.ac.uk

grammar roadblock.xtext.ibl.Ibl with org.eclipse.xtext.common.Terminals

generate ibl "http://www.xtext.roadblock/ibl/Ibl"

/////////////////////
// top level rules //
/////////////////////

// a model is composed of top level model members
Model:
	{Model}
	members+=ModelMember*
;
	
// model members are an import statement, variable definition or function definition
ModelMember:
	Import | VariableDefinition | FunctionDefinition
;

//////////////////////
// import statement //
//////////////////////

Import:
	{Import}
	'import' importedNamespace=QualifiedNameWithWildcard
;

// a variable definition
VariableDefinition:
	{VariableDefinition}
	'define' (type=ID)? name=ID
	'{'
		members+=VariableDefinitionMember*
	'}'	
;

// a function definition
FunctionDefinition:
	{FunctionDefinition}
	'define' (type=ID)? name=ID
	'('
		(parameters+=FunctionParameterMember (',' parameters+=FunctionParameterMember)*)?
	')'
	'{'
		members+=FunctionDefinitionMember*
	'}'
	(
	'USES'
		uses+=FunctionUseMember (',' uses+=FunctionUseMember)*
	)?
;

///////////////
// functions //
///////////////

// function parameter member
FunctionParameterMember:
	type=ID name=ID ':' scope=ParameterScope
;

// allowed scope of function parameters
ParameterScope:
	'required' | 'returned' | 'optional'
;

// function definition member
FunctionDefinitionMember:
	RuleDefinition | DeviceDefinition | VariableAssignment | VariableDeclaration | ATGCDefinition | PropertyDefinition
//	RuleDefinition | VariableAssignment | VariableDeclaration | DeviceDefinition | ATGCDefinition
;

// function use member
FunctionUseMember:
	type=ID name=ID
;

///////////////
// variables //
///////////////

// entries allowed inside variable definition
VariableDefinitionMember:
	VariableDeclaration
;

// a declaration of a variable
VariableDeclaration:
	{VariableDeclaration}
	(qualifier=VariableQualifier)?
	((type=ID name=ID) |
	(collection=CollectionID '<' type=ID '>' name=ID))
	(
		'=' constructor=ID '(' (parameters+=VariableAssignment (',' parameters+=VariableAssignment)*)? ')'
	)?
;

VariableQualifier:
	'observable'
;

CollectionID:
	'LIST' | 'SET'
;

VariableAssignment:
	{VariableAssignment}
	 variable=VariableAttribute '=' expression=VariableExpression
;

VariableAttribute:
	{VariableAttribute}
	name=(ID|REAL) (('.'|'~') attribute=ID)?
;

VariableExpression:
	{VariableExpression}
	members+=VariableAttribute (VariableExpressionOperator members+=VariableAttribute)*
;

VariableExpressionOperator:
	'+' | '-' | '|'
;

///////////
// rules //
///////////

// rule definition
RuleDefinition:
	{RuleDefinition}
	'RULE' name=ID ':' (lhs+=RuleObject ('+' lhs+=RuleObject)*)? ('->'|'<->') (rhs+=RuleObject ('+' rhs+=RuleObject)*)?
;

// left or right hand side of rule
RuleObject:
	{RuleObject}
	subobjects+=ID ('~' subobjects+=ID)*
;

///////////////
// processes //
///////////////

ProcessDeclaration:
	{ProcessDeclaration}
	'PROCESS' name=ID '=' constructor=ID '(' (parameters+=VariableAssignment (',' parameters+=VariableAssignment)*)? ')'
;

/////////////
// devices //
/////////////

// device definition
DeviceDefinition:
	{DeviceDefinition}
	'DEVICE' name=ID '=' 'DEVICE'
	'('
		(parts+=ID (',' parts+=ID)*)
	')'
	'('
		(parameters+=VariableAssignment (',' parameters+=VariableAssignment)*)?
	')'
	'{'
		(members+=DeviceMembers*)
	'}'
;

DeviceMembers:
	ProcessDeclaration | PropertyDefinition
;

////////////////
// atgc rules //
////////////////

ATGCDefinition:
	{ATGCDefinition}
	'ATGC' command=ID ':' arguments+=ID (',' arguments+=ID)*
;

/////////////////////////
// model checker rules //
/////////////////////////

PropertyDefinition:
	{PropertyDefinition}
	'VERIFY'
	((
		'[' property+=Property (BooleanOperator property+=Property)* ']' condition=PropertyCondition	
	) | 
	(
		'EXPECTED' '[' name=ID ']' 'AT TIME INSTANT' time=REAL 'IS' ((operator=RelationalOperator concentration=Quantity) | '?')
	))
;

Property:
	{Property}
	lhs=ID operator=RelationalOperator rhs=Quantity
;

PropertyCondition:
	{PropertyCondition}
	(
	'WILL HOLD' |
	'NEVER HOLDS' |
	'ALWAYS HOLDS'
	)
	(
	'WITHIN TIME BOUND' '[' lowerBound=REAL ',' upperBounds=REAL ']'
		(
		'WITH PROBABILITY BOUND' ((operator=RelationalOperator probability=REAL) | '?') 
		)?
	)?	 
;

/////////////////
// basic types //
/////////////////

// a real number
REAL:
	Decimal|DecimalExp
;

Decimal hidden():
	('+'|'-')?  INT ('.' INT)?
;

DecimalExp hidden():
	('+'|'-')? INT ('.' INT)? ('E'|'e') ('+'|'-')? INT
;

// a quantity with units
Quantity:
	{Quantity}
	value=REAL units=ID
;

// relational operators
RelationalOperator:
	'==' | '!=' | '<' | '>' | '<=' | '>='
;

// boolean operator
BooleanOperator:
	'&' | '|'
;

// qualified name
QualifiedNameWithWildcard:
	QualifiedName ('.*')?
;

QualifiedName:
	ID ('.' ID)*
;

