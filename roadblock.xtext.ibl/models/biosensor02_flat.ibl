///////////////////////
//  biosensor02.ibl  //
///////////////////////

// created 08/10/14 by
// Harold Fellermann, Omer Markovitch, Laurentiu Mierla, Chrsitophe Ladroue
// after discussion with Felix Dafhnis-Calas and Stephan Heeb


define SentinelCell typeof CELL(){
	rnaP = RNA() : observable

	SA = MOLECULE()
	AHL = MOLECULE()

	// diffusion rules
	RULE SADiffusion: SA -> OUTSIDE
	RULE AHLDiffusion: AHL -> OUTSIDE

	// XylS2 activation by SA
	RULE XylSActivation: XylS + SA <-> XylS~SA

	// NahR activation by SA
	RULE NahRActivation: NahR + SA <-> NahR~SA

	// LasR activation by AHL
	RULE LasRActivation: LasR + AHL <-> LasR~AHL
	
	// protein and metabolite degradations
	RULE SADegradation: SA ->
	SADegradation.forwardRate = 56.6e-6 s-1	// with an ultraviolet irradiation http://www.cder.dz/download/Art15-1_15.pdf. The value is in agreement with page 52 of http://books.google.co.uk/books?id=iFNQWsDh8w8C

	RULE AHLDegradation: AHL ->
	AHLDegradation.forwardRate = 15.4e-7 s-1	// http://onlinelibrary.wiley.com/doi/10.1111/j.1574-6941.2009.00828.x/full also http://2011.igem.org/Team:ETH_Zurich/Modeling/Parameters. In bovine serum is times 100 faster, see page 87 http://books.google.co.uk/books?id=GKx0b8HNUt4C

	RULE NahRDegradation: NahR ->
	NahRDegradation.forwardRate = 14e-6 s-1	//rough value from http://bionumbers.hms.harvard.edu//bionumber.aspx?id=110438&ver=2

	RULE LasRDegradation: LasR ->
	
	LasRDegradation.forwardRate = 14e-6 s-1	//rough value from http://bionumbers.hms.harvard.edu//bionumber.aspx?id=110438&ver=2

	RULE XylSDegradation: XylS ->
	XylSDegradation.forwardRate = 14e-6 s-1	//rough value from http://bionumbers.hms.harvard.edu//bionumber.aspx?id=110438&ver=2

	RULE LuxDegradation: LuxR ->
	LuxDegradation.forwardRate = 14e-6 s-1	//rough value from http://bionumbers.hms.harvard.edu//bionumber.aspx?id=110438&ver=2

	// components of the NahRLasRProduction device
	PnahR = PROMOTER(URI = "atgc://user-submitted/part/pnahr")
	nahR = GENE(URI = "atgc://user-submitted/part/nahr")
	lasR = GENE(URI = "atgc://user-submitted/part/lasr")
	NahR = PROTEIN()
	LasR = PROTEIN()

	DEVICE NahRLasRProduction = new DEVICE(parts=[PnahR,nahR,lasR])(input=[], output=[NahR,LasR]) {
		mrna = RNA()
		
		// constitutive transcription
		RULE promoterBinding : PnahR + rnaP <-> PnahR~rnaP			
		promoterBinding.forwardRate = 4.0e+22 M-1 s-1 // XXX right conversion? median initiation time = 15 s [Bionumbers]; 

		RULE transcription : PnahR~rnaP -> PnahR + rnaP + mrna
		transcription.forwardRate = 18.4e-3 s-1	// (1) rough transcription rate = 50 nuc/sec [Bionumbers], (2) nahR we assume 2000 nuc which is rough value for the alpha+beta subunits, (3) lasR = 720 [http://www.ncbi.nlm.nih.gov/nuccore/D30812.1]

		// protein translations
		RULE translationNahR : mrna -> mrna + NahR
		translationNahR.forwardRate = 7.96e-3	// (1) rough 15 aa/sec [Bionumbers], (2) the sum of 3*(alpha+beta) is 1884 aa [http://www.sciencemag.org/content/299/5609/1039], for simplicity we will just take this value as is

		RULE translationLasR : mrna -> mrna + LasR
		translationLasR.forwardRate = 6.25e-2 s-1	// (1) rough 15 aa/sec [Bionumbers], (2) 240 aa [http://www.uniprot.org/uniprot/P25084], in agreement with the gene length

		// rna degradation
		RULE mrnaDegradation : mrna ->
		mrnaDegradation.forwardRate = 2.778e-2 s-1	// rough 6 min [Bionumbers]
	}

	// components of the XylS2Production device
	Psal = PROMOTER(URI = "atgc://user-submitted/part/psal")
	xylS = GENE(URI = "atgc://user-submitted/part/xysl2")
	XylS = PROTEIN()

	DEVICE XylS2Production = new DEVICE(parts=[Psal,xylS])(input=[NahR,SA], output=[XylS]){
		mrna = RNA()

		// background translation
		RULE backgroundPromoterBinding : Psal + rnaP <-> Psal~rnaP	
		backgroundPromoterBinding.forwardRate = 4.0e+22 M-1 s-1	// median initiation time = 15 s [Bionumbers];

		RULE backgroundTranscription : Psal~rnaP -> Psal + rnaP + mrna
		backgroundTranscription.forwardRate = 4.52e-2 s-1	// (1) rough transcription rate = 50 nuc/sec [Bionumbers], (2) xylS=1106 nuc [http://www.ncbi.nlm.nih.gov/nuccore/NG_034502.1]
		
		// upregulated transcription
		RULE NahRBinding: Psal + NahR~SA <-> Psal~NahR~SA
		NahRBinding.forwardrate = 78.2e+3 M-1 s-1 // [Park, BBA 1725, 247 (2005)]
		NahRBinding.backwardrate = 19.1e-2 s-1 // [Park, BBA 1725, 247 (2005)]

		RULE regulatedPromoterBinding : Psal~NahR~SA + rnaP <-> Psal~NahR~SA~rnaP	
		regulatedPromoterBinding.forwardRate = 4.0e+22 M-1 s-1	// XXX why is this the same rate as background?, median initiation time = 15 s [Bionumbers];
		RULE regulatedTranscription : Psal~NahR~SA~rnaP -> Psal + NahR~SA + rnaP + mrna
		regulatedTranscription.forwardRate = 4.52e-2 s-1	// (1) rough transcription rate = 50 nuc/sec [Bionumbers], (2) xylS=1106 nuc [http://www.ncbi.nlm.nih.gov/nuccore/NG_034502.1]

		// protein translations
		RULE translation : mrna -> mrna + XylS
		translation.forwardRate = 4.67e-2 s-1	// (1) rough 15 aa/sec [Bionumbers], (2) 321 aa [Gene 44, 235 (1986)]

		RULE mrnaDegradation : mrna ->
		mrnaDegradation.forwardRate = 2.778e-2 s-1
		
		VERIFY [XylS > 0 uM ] EVENTUALLY HOLDS
	}

	// components of the LuxProduction device
	Pm = PROMOTER(URI = "atgc://user-submitted/part/pm")
	PrsaL = PROMOTER(URI = "atgc://user-submitted/part/plasr")
	luxr = GENE(URI = "http://parts.igem.org/Part:BBa_K592012")
	LuxR = PROTEIN()
 
	DEVICE LuxProduction = new DEVICE(parts=[Pm,PrsaL,luxr])(input=[XylS~SA,LasR~AHL], output=[LuxR]){
		mrna = RNA() 	
		ATGC CLONING SITES: 5
		// background transcriptions
		RULE backgroundBinding1 : Pm + rnaP <-> Pm~rnaP	
		RULE backgroundInitiation1 : Pm~rnaP -> Pm + rnaP + mrna
		RULE backgroundBinding2 : PrsaL + rnaP <-> PrsaL~rnaP	
		RULE backgroundInitiation2 : PrsaL~rnaP -> PrsaL + rnaP + mrna

		// upregulated transcription by XylS~SA
		RULE XylSBinding : XylS~SA + Pm <-> XylS~SA~Pm
		RULE XylSInitiation : XylS~SA~Pm + rnaP <-> XylS~SA~Pm~rnaP	
		RULE XylSTranscription : XylS~SA~Pm~rnaP -> Pm + XylS~SA + rnaP + mrna

		// upregulated transcription by LasR~AHL
		RULE LasRBinding : PrsaL + LasR~AHLR <-> PrsaL~LasR~AHL
		RULE LasRInitiation : PrsaL~LasR~AHL + rnaP <-> PrsaL~LasR~AHL~rnaP	
		RULE LasRTranscription : PrsaL~LasR~AHL~rnaP -> PrsaL + LasR~AHL + rnaP + mrna

		// protein translation
		RULE translation : mrna -> mrna + LuxR		
		RULE mrnaDegradation : mrna ->

		VERIFY [ [ NOT [ PrsaL > 50 uM ] ] IMPLIES [ [ rnaP < 40 uM ] AND [ LuxR < 100 uM ] ] ] EVENTUALLY HOLDS
	}

	VERIFY [SA > 0 uM] EVENTUALLY HOLDS
	VERIFY [AHL > 0 uM] EVENTUALLY HOLDS WITHIN [100,200] s
	VERIFY [NahR > 0 uM] EVENTUALLY HOLDS WITHIN [100,200] s WITH PROBABILITY >= 0.9
	VERIFY [LasR > 0 uM] NEVER HOLDS
	VERIFY [SA > 0 uM] ALWAYS HOLDS
	VERIFY [SA > 0 uM] HOLDS IN STEADY-STATE
	VERIFY [SA > 0 uM] HOLDS INFINITELY OFTEN
	VERIFY [AHL > 0 uM] WILL HOLD UNTIL THEN [SA > 0 uM]
	VERIFY EXPECTED [XylS] AT 1000 s IS ?
	VERIFY EXPECTED [XylS] WITHIN 1000 s IS ?

	ATGC ARRANGE nahR, PnahR, Psal, xylS2
}

define site typeof REGION() {
	AHL = MOLECULE()
	SA = MOLECULE()

	CELL cell = new SentinelCell()
}
