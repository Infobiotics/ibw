define RepressilatorCell typeof CELL()
{
	Plac    = PROMOTER(URI="http://parts.igem.org/Part:BBa_R0010")
	Plambda = PROMOTER(URI="http://parts.igem.org/Part:BBa_R0051")
	Ptet    = PROMOTER(URI="http://parts.igem.org/Part:BBa_R0040")

	cI      = GENE(URI="http://parts.igem.org/Part:BBa_K105004")
	lacI    = GENE(URI="http://parts.igem.org/Part:BBa_C0012")
	tetR    = GENE(URI="http://parts.igem.org/Part:BBa_C0040")
	gfp     = GENE(URI="http://parts.igem.org/Part:BBa_E0040")

	LacI   = PROTEIN()
	CI     = PROTEIN()
	TetR   = PROTEIN()
	GFP    = PROTEIN()

	RULE lac_dimerization : LacI + LacI <-> LacI~LacI
	lac_dimerization.forwardRate = 1e0 min-1
	lac_dimerization.backwardRate = 125 min-1

	RULE cI_dimerization : CI + CI <-> CI~CI
	cI_dimerization.forwardRate = 1e0 min-1
	cI_dimerization.backwardRate = 125 min-1

	RULE tetR_dimerization : TetR + TetR <-> TetR~TetR
	tetR_dimerization.forwardRate = 1e0 min-1
	tetR_dimerization.backwardRate = 125 min-1
	
	// cI degradation
	RULE cI_degradation : CI ->
	cI_degradation.forwardRate = 13.2e-1 min-1

	// LacI degradation
	RULE lac_degradation : LacI ->
	lac_degradation.forwardRate = 13.2e-1 min-1

	// TetR degradation
	RULE tetR_degradation : TetR ->
	tetR_degradation.forwardRate = 13.2e-1 min-1

	// GFP degradation
	RULE gfp_degradation : GFP ->
	gfp_degradation.forwardRate = 13.2e-2 min-1 // 13.216397448e-3 min-1 from Gardner data // 2.5e-2 min-1 // from Andersen et al. Appl Env Microbiol (1998) 64(6):2240-2246

	operon1 = DEVICE(
		parts=[Plac,tetR],
		input=[LacI~LacI],
		output=[TetR]
	)
	{
		rna = RNA()
		
		// repression
		RULE repression : Plac + LacI~LacI <-> Plac~LacI~LacI
		repression.forwardRate = 1e0 min-1
		repression.backwardRate = 10 min-1

		// transcription
		RULE transcription : Plac -> Plac + rna
		transcription.forwardRate = 1.e0 min-1

		// translation
		RULE translation : rna -> rna + TetR
		translation.forwardRate = 100 min-1
		
		// rna degradation
		RULE rna_degradation : rna ->
		rna_degradation.forwardRate = 1e-1 min-1
	}

	operon_2 = DEVICE(
		parts=[Plambda,lacI],
		input=[CI~CI],
		output=[LacI]
	)
	{
		rna = RNA()
		
		// repression
		RULE repression : Plambda + CI~CI <-> Plambda~CI~CI
		repression.forwardRate = 1e0 min-1
		repression.backwardRate = 10 min-1

		// transcription
		RULE transcription : Plambda -> Plambda + rna
		transcription.forwardRate = 1.e0 min-1

		// translation
		RULE translation : rna -> rna + LacI
		translation.forwardRate = 100 min-1
		
		// rna degradation
		RULE rna_degradation : rna ->
		rna_degradation.forwardRate = 1e-1 min-1
	}
	
	operon_3 = DEVICE(
		parts=[Ptet,cI],
		input=[TetR~TetR],
		output=[CI]
	) {
		rna = RNA()
		
		// repression
		RULE repression : Ptet + TetR~TetR <-> Ptet~TetR~TetR
		repression.forwardRate = 1e0 min-1
		repression.backwardRate = 10 min-1

		// transcription
		RULE transcription : Ptet -> Ptet + rna
		transcription.forwardRate = 1.e0 min-1

		// translation
		RULE translation : rna -> rna + CI
		translation.forwardRate = 100 min-1
		
		// rna degradation
		RULE rna_degradation : rna ->
		rna_degradation.forwardRate = 1e-1 min-1
	}
}

define site typeof REGION() {
	CELL cell = new RepressilatorCell()
}
